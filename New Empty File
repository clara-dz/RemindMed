#include <Wire.h>
#include <RTClib.h>
#include <LiquidCrystal_I2C.h>
#include <Stepper.h>

// LCD and RTC
LiquidCrystal_I2C lcd(0x27, 16, 2);
RTC_DS3231 rtc;

// Stepper setup
const int stepsPerRevolution = 2048;
Stepper stepper(stepsPerRevolution, 13, 12, 11, 10);

unsigned long lastStepTime = 0;
const unsigned long stepInterval = 10000; // 10 seconds for testing

void setup() {
  Serial.begin(9600);
  Wire.begin();

  // LCD Init
  lcd.begin(16, 2);
  lcd.backlight();
  lcd.setCursor(0, 0);
  lcd.print("Init...");

  // RTC Init
  if (!rtc.begin()) {
    lcd.setCursor(0, 1);
    lcd.print("RTC fail!");
    while (1);
  }

  // Optional: Set time (only once, then comment out)
  // rtc.adjust(DateTime(F(__DATE__), F(__TIME__)));

  // Stepper Init
  stepper.setSpeed(10);

  delay(2000);
  lcd.clear();
}

void loop() {
  DateTime now = rtc.now();

  // Display current time
  char timeStr[17];
  snprintf(timeStr, sizeof(timeStr), "Time %02d:%02d:%02d",
           now.hour(), now.minute(), now.second());
  lcd.setCursor(0, 0);
  lcd.print(timeStr);

  // Simulate scheduled dispense every 10 seconds
  if (millis() - lastStepTime >= stepInterval) {
  lastStepTime = millis();

  lcd.setCursor(0, 1);
  lcd.print("Dispensing...   ");
  stepper.step(256);  // 45-degree rotation
  lcd.setCursor(0, 1);
  lcd.print("Done rotating   ");
}


  delay(500);
}

